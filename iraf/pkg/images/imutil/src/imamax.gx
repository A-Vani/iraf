# Copyright(c) 1986 Association of Universities for Research in Astronomy Inc.

include	<imhdr.h>

# IMA_MAX -- Image arithmetic maximum value.

$for (silrd)
procedure ima_max$t (im_a, im_b, im_c, a, b)

pointer	im_a, im_b, im_c
PIXEL	a, b

size_t	sz_val
long	l_val
size_t	len
pointer	im[3], buf[3]
long	v[IM_MAXDIM, 3]

int	ima_nl$t()

begin
	# Loop through all of the image lines.
	im[1] = im_c
	len = IM_LEN (im[1], 1)
	l_val = 1
	sz_val = 3 * IM_MAXDIM
	call amovkl (l_val, v, sz_val)

	# If imagea is constant then read imageb and do the vector/scalar
	# maximum to imagec.
	if (im_a == NULL) {
	    im[2] = im_b
	    while (ima_nl$t (im, buf, v, 2) != EOF)
		call amaxk$t (Mem$t[buf[2]], a, Mem$t[buf[1]], len)

	# If imageb is constant then read imagea and do the vector/scalar
	# maximum to imagec.
	} else if (im_b == NULL) {
	    im[2] = im_a
	    while (ima_nl$t (im, buf, v, 2) != EOF)
		call amaxk$t (Mem$t[buf[2]], b, Mem$t[buf[1]], len)

	# Read imagea and imageb and do a vector-vector maximum
	# operation to imagec.
	} else {
	    im[2] = im_a
	    im[3] = im_b
	    while (ima_nl$t (im, buf, v, 3) != EOF)
		call amax$t (Mem$t[buf[2]], Mem$t[buf[3]], Mem$t[buf[1]], len)
	}
end
$endfor
